buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.flywaydb:flyway-mysql:9.5.0'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'org.springframework.boot' version '2.7.18'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'org.flywaydb.flyway' version '9.5.0'
}

group 'com.github.allisson95.codeflix'
version '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':domain')
    implementation project(':application')

    runtimeOnly 'com.mysql:mysql-connector-j'

    implementation platform('org.testcontainers:testcontainers-bom:1.17.5')

    implementation 'io.vavr:vavr:0.10.4'

    implementation 'org.springdoc:springdoc-openapi-webmvc-core:1.6.12'
    implementation 'org.springdoc:springdoc-openapi-ui:1.6.12'

    implementation 'com.fasterxml.jackson.module:jackson-module-afterburner'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude module: 'spring-boot-starter-tomcat'
    }
    implementation 'org.springframework.boot:spring-boot-starter-undertow'

    testImplementation 'net.datafaker:datafaker:1.9.0'

    testImplementation 'org.flywaydb:flyway-core'
    testImplementation 'org.flywaydb:flyway-mysql'

    testImplementation 'org.testcontainers:testcontainers'
    testImplementation 'org.testcontainers:mysql'
    testImplementation 'org.testcontainers:junit-jupiter'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    testRuntimeOnly 'com.h2database:h2'
}

flyway {
    url = System.getenv('FLYWAY_DB') ?: 'jdbc:mysql://db:3306/admin_catalogo'
    user = System.getenv('FLYWAY_USER') ?: 'root'
    password = System.getenv('FLYWAY_PASS') ?: 'root'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('bootJar') {
    archiveName 'application.jar'
    destinationDirectory.set(file("${rootProject.buildDir}/libs"))
}
